plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm' version '1.4.31'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.14.0'
}

group = 'de.tzander.gradle.simplesematicversioning'
version = '0.7.0'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
}

pluginBundle {
    website = 'https://github.com/torstenzander/simple-semantic-versioning-plugin'
    vcsUrl = 'https://github.com/torstenzander/simple-semantic-versioning-plugin.git'
    tags = ['version', 'semantic', 'increase']
}

gradlePlugin {
    plugins {
        simplesematicversioning {
            id = 'de.tzander.gradle.simplesematicversioning'
            displayName = 'Simple Semantic Versioning Plugin'
            description = 'This plugin increases semantic versions by one. You can define additional files.'
            implementationClass = 'de.tzander.gradle.simplesematicversioning.SimpleSemanticVersioningPlugin'
        }
    }
}

sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

tasks.named('check') {
    dependsOn(tasks.functionalTest)
}


